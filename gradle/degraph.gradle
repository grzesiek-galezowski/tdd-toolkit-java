buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "de.schauderhaft.degraph:degraph-check:${degraphVersion}"
        classpath "org.hamcrest:hamcrest-all:1.3"
    }
}

import static de.schauderhaft.degraph.check.JCheck.customClasspath
import static de.schauderhaft.degraph.check.JCheck.violationFree
import static org.hamcrest.MatcherAssert.assertThat
import static org.hamcrest.Matchers.is

import de.schauderhaft.degraph.configuration.NamedPattern

task degraph(type: DegraphCheck) {
    reportFile = new File(buildDir, "reports/degraph/testResult.graphml")
    classpath = sourceSets.main.output // TODO Replace with: files(sourceSets.main.java.outputDir) ?
}

check.dependsOn degraph

class DegraphCheck extends DefaultTask {

    @InputFiles
    FileCollection classpath

    @OutputFile
    File reportFile

    @TaskAction
    void runDegraph() {
        def graph = customClasspath(classpath.asPath)
                .printTo(reportFile.getPath())
                .noJars()
                .including("com.github.grzesiek_galezowski.test_environment.**")
        assertThat(graph, is(violationFree()))
    }

}